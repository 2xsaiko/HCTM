import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

import static org.gradle.api.JavaVersion.VERSION_1_8

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.30'
    id 'fabric-loom' version '0.2.5-SNAPSHOT' apply false
}

allprojects {
    apply plugin: 'fabric-loom'

    java {
        sourceCompatibility = VERSION_1_8
        targetCompatibility = VERSION_1_8
    }

    tasks.withType(KotlinCompile) {
        kotlinOptions.jvmTarget = '1.8'
        kotlinOptions.freeCompilerArgs += '-Xuse-experimental=kotlin.ExperimentalUnsignedTypes'
        kotlinOptions.freeCompilerArgs += '-Xjvm-default=enable'
    }

    group = "therealfarfetchd.hctm"

    dependencies {
        minecraft group: "com.mojang", name: "minecraft", version: minecraft_version
        mappings group: "net.fabricmc", name: "yarn", version: mappings_version
        modCompile group: "net.fabricmc", name: "fabric-loader", version: loader_version

        modCompile group: "net.fabricmc.fabric-api", name: "fabric-api", version: fabric_api_version
        compile(group: "net.fabricmc", name: "fabric-language-kotlin", version: fabric_kotlin_version) {
            exclude module: 'fabric-loader'
        }
        compileOnly group: "org.jetbrains.kotlin", name: "kotlin-stdlib", version: kotlin_version
        compileOnly group: "org.jetbrains.kotlin", name: "kotlin-stdlib-jdk8", version: kotlin_version

        compile group: "org.joml", name: "joml", version: "1.9.14"
    }

    configurations {
        dev
    }

    repositories {
        // mavenLocal()
        maven { url 'https://maven.therealfarfetchd.dev' }
        // maven { url 'https://maven.shadowfacts.net' }
        // maven { url 'https://minecraft.curseforge.com/api/maven' }
        // maven { url 'https://grondag-repo.appspot.com'; credentials { username 'guest'; password '' } }
    }

    afterEvaluate {
        artifacts {
            dev file: file("${project.buildDir}/libs/$archivesBaseName-${version}-dev.jar"), type: "jar", builtBy: remapJar
        }

        processResources {
            inputs.property "version", project.version

            from(sourceSets.main.resources.srcDirs) {
                include "fabric.mod.json"
                expand "version": project.version
            }

            from(sourceSets.main.resources.srcDirs) {
                exclude "fabric.mod.json"
            }
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

subprojects {
    task remapMavenJar(type: Copy, dependsOn: remapJar) {
        afterEvaluate {
            from("${project.buildDir}/libs/$archivesBaseName-${version}.jar")
            into("${project.buildDir}/libs/")
            rename { String fn -> "$archivesBaseName-${version}-maven.jar" }
        }
    }
}

task remapMavenJar(type: net.fabricmc.loom.task.RemapJarTask, dependsOn: jar) {
    afterEvaluate {
        input = file("${project.buildDir}/libs/${archivesBaseName}-${version}-dev.jar")
        archiveName = "${archivesBaseName}-${version}-maven.jar"
        addNestedDependencies = false
    }
}

dependencies {
    afterEvaluate {
        subprojects.each { compile it }
    }
}