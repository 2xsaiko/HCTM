import static org.gradle.api.JavaVersion.VERSION_1_8

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
    id 'fabric-loom' version '0.2.7-SNAPSHOT' apply false
}

allprojects {
    apply plugin: 'fabric-loom'

    java {
        sourceCompatibility = VERSION_1_8
        targetCompatibility = VERSION_1_8
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
        kotlinOptions.jvmTarget = '1.8'
        kotlinOptions.freeCompilerArgs += '-Xuse-experimental=kotlin.ExperimentalUnsignedTypes'
        kotlinOptions.freeCompilerArgs += '-Xjvm-default=enable'
    }

    group = "therealfarfetchd.hctm"

    dependencies {
        minecraft group: "com.mojang", name: "minecraft", version: minecraft_version
        mappings group: "net.fabricmc", name: "yarn", version: mappings_version, classifier: "v2"
        modImplementation group: "net.fabricmc", name: "fabric-loader", version: loader_version

        modImplementation group: "net.fabricmc.fabric-api", name: "fabric-api", version: fabric_api_version
        implementation(group: "net.fabricmc", name: "fabric-language-kotlin", version: fabric_kotlin_version) {
            exclude module: 'fabric-loader'
        }

        modImplementation group: 'net.dblsaiko', name: 'libaddict', version: libaddict_version

        compile group: "org.joml", name: "joml", version: "1.9.14"
    }

    configurations {
        dev
    }

    repositories {
        // mavenLocal()
        maven { url 'https://maven.dblsaiko.net' }
        // maven { url 'https://maven.shadowfacts.net' }
        // maven { url 'https://minecraft.curseforge.com/api/maven' }
        // maven { url 'https://grondag-repo.appspot.com'; credentials { username 'guest'; password '' } }
    }

    afterEvaluate {
        artifacts {
            dev file: file("${project.buildDir}/libs/$archivesBaseName-${version}-dev.jar"), type: "jar", builtBy: remapJar
        }

        processResources {
            inputs.property "version", project.version

            from(sourceSets.main.resources.srcDirs) {
                include "fabric.mod.json"
                expand "version": project.version
            }

            from(sourceSets.main.resources.srcDirs) {
                exclude "fabric.mod.json"
            }
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

subprojects {
    task remapMavenJar(type: Copy, dependsOn: remapJar) {
        afterEvaluate {
            from("${project.buildDir}/libs/$archivesBaseName-${version}.jar")
            into("${project.buildDir}/libs/")
            rename { String fn -> "$archivesBaseName-${version}-maven.jar" }
        }
    }
}

task remapMavenJar(type: net.fabricmc.loom.task.RemapJarTask, dependsOn: jar) {
    afterEvaluate {
        input = file("${project.buildDir}/libs/${archivesBaseName}-${version}-dev.jar")
        archiveName = "${archivesBaseName}-${version}-maven.jar"
        addNestedDependencies = false
    }
}

repositories {
    maven { url = 'https://maven.dblsaiko.net' }
}

dependencies {
    modRuntime group: 'net.dblsaiko', name: 'winwonders', version: '0.1.2-4'
    modRuntime group: 'io.github.prospector', name: 'modmenu', version: '1.8.5+build.23'

    afterEvaluate {
        subprojects.each { compile it }
    }
}